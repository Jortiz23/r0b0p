r0b0p {

  Program     = Statement*
  Statement   = CompStmt
              | SimpStmt ";" --simple
  SimpStmt    = Assignment
              | Return
              | FuncCall
              | Print
  CompStmt    = FuncDecl
              | WhileLoop
              | ForLoop
              | Conditional
  Block       = "<" Statement* ">"
  Exp         = Exp andorop Exp1 --binary
              | Exp1
  Exp1        = Exp1 relop Exp2 --binary
              | Exp2
  Exp2        = Exp2 addop Exp3 --binary
              | Exp3
  Exp3        = Exp3 mulop Exp4 --binary
              | Exp4
  Exp4        = "-" Exp5 --negation
              | Exp5
  Exp5        = Exp6 expop Exp5 --binary
              | Exp6
  Exp6        = "N0T" Exp7 --not
              | Exp7
  Exp7        = "(" Exp ")" --parens
              | FuncCall
              | id
              | random
              | number
              | text
              | boolean
              | List
              | Dict
              | undefined
              
  Return      = "G1V3" Exp
  Assignment  = id "=" Exp

  WhileLoop   = "WH1L3" "[" Exp "]" Block
  ForLoop     = "C0UNT" "[" (id ":")? (number | id) "->" (number | id) "]" Block

  FuncDecl    = "PR0GRAM" id "[" (ListOf<id, ",">)? "]" Block
  FuncCall    = (id | mathFunc | listFunc | dictFunc) "[" (ListOf<Exp, ",">)? "]"
  Conditional = "PR3SUM1NG" "[" Exp "]" Block ElseIfBlock* ElseBlock?
  ElseIfBlock = "3LS3 1F" "[" Exp "]" Block
  ElseBlock   = "3LS3" Block
  Print       = "SP3AK" "[" (Exp) "]"

  List        = "{" (ListOf<Exp, ",">)? "}"
  Dict        = "{" ListOf<KeyValue, ",">? "}"
  KeyValue    = id ":" Exp
  
  id          = ~keyword letter (alnum | "_")*
  keyword     = ("N0T" | "b00l" | "l3tt3r" | "d1g1ts" | "uncl3ar" | "t3xt" | "l1st"
              | "d1ct" | "SP3AK" | "C0UNT" | "WH1L3" | "G1V3" | "PR0GRAM" | "b0p"
              | "b1p" | "..." | "3LS3" | "3LS3 1F" | "UNPR3D1CTABL3"
              | mathFunc | listFunc | dictFunc)
  mathFunc    = "SQRT" | "ABS" | "FLOOR" | "POW"
  listFunc    = "PLAC3_AT" | "D1SCARD_AT" | "R3TR13V3_AT" | "SUBST1TUT3" | "S1Z3"
  dictFunc    = "PLAC3" | "D1SCARD" | "R3TR13V3" | "C0D3S" | "SUBST1TUT3"

  number      = decimal 
              | digit+                       -- integer
  decimal     = digit* "." digit+
  char        = ~"\'" ~"\"" ~"\n" ~"\\" any
              | escape
  text        = "\"" char* "\""
  boolean     = "b0p" | "b1p"
  undefined   = "uncl3ar"

  escape      = "\\" ("\'" | "\"" | "n" | "t" | "\\" | codepoint)
  codepoint   = "u{" hexDigit+ "}"
  comment     =  "..." (~"\n" any)*
  random      = "UNPR3D1CTABL3"

  mulop       = "*" | "/" | "%"
  expop       = "**"
  relop       = ">=" | "<=" | "<" | ">" | "=="
  addop       = "+" | "-"
  andorop     = "||" | "&&"

  space      :=  " " | "\t" | "\n" | comment
}

